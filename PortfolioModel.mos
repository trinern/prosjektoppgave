model PortfolioModel
uses "mmxprs"; 
 
options explterm, noimplicit;

parameters
	DataFile = 'PortfolioData.txt'; !'PortfolioData.txt' or 'PortfolioDataTest.txt'
	LoadFile = 'LoadData.txt';!'LoadData.txt' or 'LoadDataTest.txt'
	IDPriceFile = 'IDPriceData.txt';	!IntraDayPriceFactor
	ForecastFile = 'ForecastData.txt'; !'ForecastData.txt' or 'ForecastDataTest.txt' test: 48 h x 2
	ErrorFile = 'ForecastErrorData.txt';
	DAPriceFile = 'DAPriceData.txt';
	InFlowFile = 'InFlow.txt';
	original = true;
	SOSimplementation = false;	
	Presolve = true;
	Heuristic = true;
	Cutting = 'Yes';
	Print = 'True';
end-parameters

declarations
  timetracker:	real; ! used to log timestamps for time consumption output
end-declarations

writeln("Building model...");
timetracker := timestamp; ! assigns current "timestamp" to timetracker

!Declarations of sets
declarations
	nSeasons:		integer;
	nNodes:			integer;
	nHours:			integer;
	nPlants:		integer;	
	nScenarios:		integer;
	nErrors:		integer;
end-declarations
	
initializations from DataFile
	nSeasons;
	nNodes;
	nHours;
	nPlants;	
	nScenarios;
	nErrors;
end-initializations
	
declarations
	Seasons:		set of integer;
	Days:			set of integer;	!Days where decitions are made (1,2,3)
	Nodes:			set of integer; !Set of nodes in tree
	Hours:			set of integer; !hours in each year in horizon
	Plants:			set of integer;	!all identified power production plants
	Scenarios:		set of integer;	!first level uncertainty, given as paths until leaf nodes
	Errors:			set of integer;	!2nd level uncertainty
	CapPlants:		set of integer;	!power production projects with reliable capacity 
	WindPlants:		set of integer;	!wind plants
	ThermalPlants:	set of integer;	!set of thermal plants
	HydroPlants:	set of integer; !consider removing
end-declarations	

initializations from DataFile
	Days;
	CapPlants;
	WindPlants;
	ThermalPlants;
	HydroPlants;
end-initializations

Seasons		:= 1 .. nSeasons;!p
Nodes		:= 1 .. nNodes;	!n
Hours		:= 1 .. nHours;		!h
Plants 		:= 1 .. nPlants;	!i
Scenarios 	:= 1 .. nScenarios;	!s
Errors		:= 1 .. nErrors;	!w

finalize(Seasons);
finalize(Nodes);
finalize(Hours);
finalize(Plants);
finalize(Scenarios);
finalize(Errors);

!Declarations of parameters in matrix form	
declarations
	Demand:			array(Seasons,Days,Hours)	of real;	!forcasted demand in hour h
	InvCost:		array(Plants)				of integer;
	OprCost:		array(Plants)				of real;
	FixedOprCost:	array(Plants)				of integer;	!yearly operating cost
	UpperCap:		array(Plants)				of integer; !upper limit on cap installation
	StartUpCost:	array(ThermalPlants)		of integer;
	UpperRamping:	array(ThermalPlants)		of real;	!max ramping from h to h+1 thermal plants
	LowerThermal:	array(ThermalPlants)		of real;	!lower running capacity for thermal plants
	!ProbS:			array(Scenarios)			of real;	!probability for scenario s
	ProbE:			array(Errors)				of real;	!probability for error w
	IDPriceFactor:	array(Seasons,Days,Hours)	of	real;	!load specific intra-day price
	ForecastError:	array(Errors)				of real;
	DayAheadPrice:	array(Seasons,Days,Hours)	of real;
	EstimatedProd:	array(Days,Hours,Scenarios,Seasons)	of real;!wind power production in scenario s, hour h
	OprHours:		array(Plants)				of real;
	ReservoirEnergy:array(HydroPlants)			of real;	!energy in reservoir at beginning of period (and end)
	InFlow:			array(Seasons,HydroPlants)	of integer;
end-declarations

declarations
	ProbS:				real;
end-declarations
	
initializations from DataFile
	InvCost;
	OprCost;
	FixedOprCost;
	UpperCap;
	StartUpCost;
	UpperRamping;
	LowerThermal;
	OprHours;
	ReservoirEnergy;
end-initializations

initializations from LoadFile
	Demand;
end-initializations

initializations from IDPriceFile
	IDPriceFactor;
end-initializations

initializations from ForecastFile
	ProbS;
	EstimatedProd;
end-initializations

initializations from ErrorFile
	ProbE;
	ForecastError;
end-initializations

initializations from DAPriceFile
	DayAheadPrice;
end-initializations

initializations from InFlowFile
	InFlow;
end-initializations

!Constants
declarations
	CapPrice:			real;
	Penalty:			integer;
	IDPriceConst:		real;	!wind power production const in price expression
	TransCostDayAhead:	real;	!proportional transaction cost day-ahead market
	TransCostIntraDay:	real;	!proportional transaction cost balance market, assumed equal for sales and purchases
	!DayAheadFee:		integer;!yearly participation fee day-ahead market
	!IntraDayFee:		integer;!yearly participation fee balance market
	LowerStressLimit:	real;	!definition for system stress at scenario s in hour h	
	PumpEff:			real;!efficiency of pump storage
	ScaleFactor:		integer;
	TotalWindCap:		integer;	!total installed wind power capacity in market
	ScenariosDay1:		integer; !Number of scenarios day one (second stage)
end-declarations
	
initializations from DataFile
	CapPrice;
	Penalty;
	IDPriceConst;
	TransCostDayAhead;
	TransCostIntraDay;
	!DayAheadFee;
	!IntraDayFee;
	LowerStressLimit;
	PumpEff;
	ScaleFactor;
	TotalWindCap;
	ScenariosDay1;
end-initializations

!Variables
declarations	
	investedCap: 			dynamic array (Plants, Scenarios) of mpvar;	!cap invested in plant i
	investedCapAnt:			dynamic array (Plants) of mpvar; !Non-anticipativity helping variable
	chosenPlant: 			dynamic array (Plants, Scenarios) of mpvar;	!binary var = 1 if plant i is chosen for investment
	chosenPlantAnt: 			dynamic array(Plants) of mpvar; !Non-anticipativity helping variable
	soldCap: 				dynamic array (CapPlants, Scenarios) of mpvar;	!cap sold in cap market from plant i in year t
	soldCapAnt:				dynamic array (CapPlants) of mpvar;	

	soldDayAhead: 			dynamic array(Seasons,Days, Hours, Scenarios) of mpvar;	!energy sold in day-ahead market for year t, hour h and 2nd stage scenario s
	soldDayAheadAnt: 		dynamic array(Seasons, Nodes, Hours) of mpvar;

	production: 	dynamic array (Seasons,Days, Hours, Plants, Scenarios, Errors) of mpvar;	!energy produced from plant i
	soldIntraDay: 	dynamic array (Seasons,Days, Hours, Scenarios, Errors) of mpvar;	!energy sold in intra-day market
	purchIntraDay:	dynamic array (Seasons,Days, Hours, Scenarios, Errors) of mpvar;	!energy purchased in intra-day market
	unmetCap: 		dynamic array (Seasons,Days, Hours, Plants, Scenarios, Errors) of mpvar;	!energy not meeting capacity liabilities. Assuming system stress lasting 1 h
	pumpStorage:	dynamic array (Seasons,Days, Hours, HydroPlants, Scenarios, Errors) of mpvar;
	isRunning:		dynamic array(Seasons,Days, Hours, ThermalPlants, Scenarios, Errors) of mpvar;
	startedAt:		dynamic array(Seasons,Days, Hours, ThermalPlants, Scenarios, Errors) of mpvar;
	stored:			dynamic array(Seasons,Days, Hours, HydroPlants, Scenarios, Errors) of mpvar; !Energy stored in reservoir at end of hour h
	spilled:		dynamic array(Seasons,Days, Hours, HydroPlants, Scenarios, Errors) of mpvar;		
end-declarations

forall (ii in Plants, ss in Scenarios) do
	create (investedCap(ii,ss));
	create (chosenPlant(ii,ss));
	chosenPlant(ii,ss) is_binary;
end-do

forall (ii in Plants) do
	create (investedCapAnt(ii));
	create (chosenPlantAnt(ii));
	chosenPlantAnt(ii) is_binary;
end-do

forall (ii in CapPlants, ss in Scenarios) do
	create (soldCap(ii,ss));
end-do

forall (ii in CapPlants) do
	create (soldCapAnt(ii));
end-do

forall (pp in Seasons,dd in Days,hh in Hours, ss in Scenarios) do !consider preprocessing
	create (soldDayAhead(pp,dd ,hh, ss));
end-do

forall (pp in Seasons,nn in Nodes, hh in Hours) do 
	create (soldDayAheadAnt(pp,nn,hh));
end-do

forall (pp in Seasons,dd in Days,hh in Hours, ii in Plants, ss in Scenarios, ww in Errors) do !consider preprocessing
	create (production(pp,dd, hh, ii, ss, ww));
end-do

forall (pp in Seasons,dd in Days, hh in Hours, ss in Scenarios, ww in Errors) do !consider preprocessing
	create (soldIntraDay(pp,dd, hh, ss, ww));
	create (purchIntraDay(pp,dd, hh, ss, ww));
end-do

forall (pp in Seasons,dd in Days, hh in Hours, ii in ThermalPlants, ss in Scenarios, ww in Errors) do
	create (startedAt(pp,dd, hh, ii, ss, ww));
	startedAt(pp,dd,hh,ii,ss,ww) is_binary;
	create (isRunning(pp,dd, hh, ii, ss, ww));
	isRunning(pp,dd,hh,ii,ss,ww) is_binary;
end-do

forall (pp in Seasons, dd in Days,hh in Hours, ii in HydroPlants, ss in Scenarios, ww in Errors) do
	create (pumpStorage(pp,dd,hh, ii, ss, ww));
end-do

forall (pp in Seasons, dd in Days, hh in Hours, ii in CapPlants, ss in Scenarios, ww in Errors) do !consider preprocessing
	create (unmetCap(pp,dd, hh, ii, ss, ww));
end-do

forall (pp in Seasons, dd in Days,hh in Hours, ii in HydroPlants, ss in Scenarios, ww in Errors) do
	create(stored(pp,dd,hh,ii,ss,ww));
	create(spilled(pp,dd,hh,ii,ss,ww));
end-do

!Constraints
declarations
	NetPresentValue:	linctr;!obj value
	CapacityCon:		dynamic array (CapPlants, Scenarios) of linctr;!available reliable capacity for sales in cap market
	ProductionConWind:	dynamic array (Seasons,Days,Hours, WindPlants, Scenarios, Errors) of linctr;!upper bound energy production form wind plants
	ProductionConStable:dynamic array (Seasons, Days, Hours, Plants, Scenarios, Errors) of linctr;!upper bound energy production. Except hydro?
	ProductionThermal:	dynamic array (Seasons, Days, Hours, ThermalPlants, Scenarios, Errors) of linctr;
	StartingCon:		dynamic array (Seasons, Days, Hours, ThermalPlants, Scenarios, Errors) of linctr;
	RampingCon:			dynamic array (Seasons, Days, Hours, ThermalPlants, Scenarios, Errors) of linctr;
	HydroCon:			dynamic array (Seasons, Days, Hours, HydroPlants, Scenarios, Errors) of linctr;!hydropower constraint
	HydroConEnd:		dynamic array (Seasons, Days,HydroPlants,Scenarios,Errors) of linctr;
	InvestmentCon:		dynamic array (Plants, Scenarios) of linctr;
	ProdSalesBalance:	dynamic array (Seasons,Days, Hours, Scenarios, Errors) of linctr;	!balancing production, sales, purchases, pumping
	StressBalance:		dynamic array (Seasons,Days, Hours, CapPlants, Scenarios, Errors) of linctr;!balancing production, sales and unmet cap liabilities during system stress
	LowLimThermal:		dynamic array (Seasons,Days, Hours, ThermalPlants, Scenarios, Errors) of linctr; ! lower limit production thermal plants
	Anticipativity1:	dynamic array(Plants, Scenarios) of linctr;
	Anticipativity2:	dynamic array(Plants, Scenarios) of linctr;
	Anticipativity3:	dynamic array(Plants, Scenarios) of linctr;
	Anticipativity4:	dynamic array(Seasons,Nodes, Days, Hours, Scenarios) of linctr;
end-declarations

NetPresentValue :=
	sum (ss in Scenarios)ProbS*(
		-
		sum (ii in Plants) InvCost(ii)*investedCap(ii,ss)
		+
		sum (ii in CapPlants) CapFactor*(CapPrice*soldCap(ii,ss)-FixedOprCost(ii)*chosenPlant(ii,ss))
		+ 
		sum (pp in Seasons, dd in Days, hh in Hours) CapFactor*ScaleFactor*soldDayAhead(pp,dd,hh,ss)*(DayAheadPrice(pp,dd,hh)-TransCostDayAhead)
		+
		sum (pp in Seasons, dd in Days, hh in Hours, ww in Errors) ProbE(ww)*
		(
			soldIntraDay(pp,dd,hh,ss,ww)*(IDPriceFactor(pp,dd,hh)*Demand(pp,dd,hh)+IDPriceConst-TransCostIntraDay) 
			- purchIntraDay(pp,dd,hh,ss,ww)*(IDPriceFactor(pp,dd,hh)*Demand(pp,dd,hh)+IDPriceConst-TransCostIntraDay) 
			- sum (ii in Plants)OprCost(ii)*production(pp,dd,hh,ii,ss,ww)
			- sum (ii in ThermalPlants) StartUpCost(ii)*startedAt(pp,dd,hh,ii,ss,ww)
			- sum (ii in CapPlants) Penalty*unmetCap(pp,dd,hh,ii,ss,ww)
		)
	);
				
forall(ii in CapPlants, ss in Scenarios) do
	CapacityCon(ii,ss):=
		soldCap(ii,ss) - investedCap(ii,ss) <= 0;	!capacity sold from plant i in CM in year t cant be more than max capacity at cap plant i
end-do

forall(pp in Seasons, dd in Days, hh in Hours, ii in WindPlants, ss in Scenarios, ww in Errors) do
	ProductionConWind(pp,dd,hh,ii,ss,ww):=
		production(tt,pp,dd, hh,ii,ss,ww) - (EstimatedProd(dd,hh,ss,pp)+ ForecastError(ww))*OprHours(ii)*investedCap(ii,ss) = 0;	!Upper bound intermittent production, scenario dependent
end-do

forall(pp in Seasons, dd in Days, hh in Hours, ii in Plants | Plants <> WindPlants, ss in Scenarios, ww in Errors) do
	ProductionConStable(pp,dd,hh,ii,ss,ww):=
		production(pp,dd,hh,ii,ss,ww) - OprHours(ii)*investedCap(ii,ss) <= 0;	!Upper bound stable production
end-do

forall (pp in Seasons, dd in Days, hh in Hours, ii in ThermalPlants, ss in Scenarios, ww in Errors) do
	ProductionThermal(pp,dd,hh,ii,ss,ww):=
		production(pp,dd,hh,ii,ss,ww) - UpperCap(ii)*isRunning(pp,dd,hh,ii,ss,ww) <= 0;
end-do

forall (pp in Seasons, dd in Days, hh in Hours | hh <>1, ii in ThermalPlants, ss in Scenarios, ww in Errors) do
	StartingCon(pp,dd,hh,ii,ss,ww):=
		isRunning(pp,dd,(hh-1),ii,ss,ww) + startedAt(pp,dd,hh,ii,ss,ww) - isRunning(pp,dd,hh,ii,ss,ww) = 0;
end-do

forall (pp in Seasons, dd in Days, ii in ThermalPlants, ss in Scenarios, ww in Errors) do
	StartingCon(pp,dd,1,ii,ss,ww):=
		startedAt(pp,dd,1,ii,ss,ww) - isRunning(pp,dd,1,ii,ss,ww) = 0;
end-do

forall(pp in Seasons, dd in Days, hh in Hours | hh <>1, ii in ThermalPlants, ss in Scenarios, ww in Errors) do
	RampingCon(pp,dd,hh,ii,ss,ww):=
		production(pp,dd,hh,ii,ss,ww) - production(pp,dd,(hh-1),ii,ss,ww) - UpperRamping(ii)*(investedCap(ii,ss)) <= 0;	!max ramping when production is increasing from h to h+1 when, given as share of max capacity
end-do

forall(pp in Seasons,dd in Days, ii in ThermalPlants, ss in Scenarios, ww in Errors) do
	RampingCon(pp,dd,1,ii,ss,ww):=
		production(pp,dd,1,ii,ss,ww) - UpperRamping(ii)*investedCap(ii,ss) <= 0;	!ramping constraint first hour in period
end-do

forall(pp in Seasons, dd in Days, hh in Hours | hh <>1 , ii in HydroPlants, ss in Scenarios, ww in Errors) do
	HydroCon(pp,dd,hh,ii,ss,ww):=
		stored(pp,dd,hh,ii,ss,ww)-stored(pp,dd,(hh-1),ii,ss,ww) - PumpEff*pumpStorage(pp,dd,hh,ii,ss,ww) 
		+ production(pp,dd,hh,ii,ss,ww) + spilled(pp,dd,hh,ii,ss,ww) 
		= InFlow(pp,ii);
end-do
		
forall(pp in Seasons, dd in Days, ii in HydroPlants, ss in Scenarios, ww in Errors) do
	HydroCon(pp,dd,1,ii,ss,ww):=
		stored(pp,dd,1,ii,ss,ww) - PumpEff*pumpStorage(pp,dd,1,ii,ss,ww) 
		+ production(pp,dd,1,ii,ss,ww) + spilled(pp,dd,1,ii,ss,ww) 
		= ReservoirEnergy(ii) + InFlow(pp,ii);
end-do

forall(pp in Seasons, dd in Days, ii in HydroPlants, ss in Scenarios, ww in Errors) do
	HydroConEnd(pp,dd,ii,ss,ww):=
		stored(pp,dd,24,ii,ss,ww)  = ReservoirEnergy(ii);
end-do

forall(ii in Plants, ss in Scenarios) do
	InvestmentCon(ii,ss):=
		investedCap(ii,ss) - UpperCap(ii)*chosenPlant(ii,ss) <= 0;
end-do

forall(pp in Seasons, dd in Days, hh in Hours, ss in Scenarios, ww in Errors) do
	ProdSalesBalance(pp,dd,hh,ss,ww):=												!Balance for energy produced, stored, sold and purchased
		+ purchIntraDay(pp,dd,hh,ss,ww)- soldIntraDay(pp,dd,hh,ss,ww)
		- soldDayAhead(pp,dd,hh,ss)
		- sum(ii in HydroPlants) stored(pp,dd,hh,ii,ss,ww)
		+ sum(ii in Plants)production(pp,dd,hh,ii,ss,ww)
		= 0;	
end-do		
				
forall (pp in Seasons,dd in Days, hh in Hours, ii in CapPlants, ss in Scenarios | (Demand(pp,dd,hh)-TotalWindCap*EstimatedProd(dd,hh,ss,pp))/Demand(pp,dd,hh) > LowerStressLimit, ww in Errors) do !test for system stress, can use another value for wind power prod
	StressBalance(pp,dd,hh,ii,ss,ww):=
		production(pp,dd,hh,ii,ss,ww) + unmetCap(pp,dd,hh,ii,ss,ww) - soldCap(ii,ss) = 0; ! if sold cap from i must be met by production on plant i
end-do

(!
forall (tt in Years, hh in Hours, ss in Scenarios | (Demand(hh)-EstimatedProd(ss,hh)) > LowerStressLimit, ww in Errors) do !test for system stress, can use another value for wind power prod
	StressBalance(tt,hh,ss,ww):=
		sum (ii in Plants) production(tt,hh,ii,ss,ww) + unmetCap(tt,hh,ss,ww) - sum(jj in CapPlants)soldCap(tt,jj) = 0;	!if sold cap from plant i can be met by production on any plant
end-do
!)

forall(pp in Seasons, dd in Days, hh in Hours, ii in ThermalPlants, ss in Scenarios, ww in Errors) do
	LowLimThermal(pp,dd,hh,ii,ss,ww):=
		production(pp,dd,hh,ii,ss,ww) >= LowerThermal(ii)*OprHours(ii);
end-do	

!Non-anticipativity constraints

forall(ii in Plants, ss in Scenarios) do
	Anticipativity1(ii,ss):=
	 	investedCap(ii,ss) = investedCapAnt(ii);
end-do

forall(ii in Plants, ss in Scenarios) do
	Anticipativity2(ii,ss):=
	 	chosenPlant(ii,ss) = chosenPlantAnt(ii);
end-do

forall(ii in CapPlants, ss in Scenarios) do
	Anticipativity3(ii,ss):=
	 	soldCap(ii,ss) = soldCapAnt(ii);
end-do

!må fikses
!antall noder in Nodes er noder som har day-ahead beslutninger 
forall(pp in Seasons, hh in Hours, ss in Scenarios) do
	Anticipativity4(pp,1,1,hh,ss):=
	 	soldDayAhead(pp,1,hh,ss) = soldDayAheadAnt(pp,1,hh);
end-do

!må fikses
forall(pp in Seasons, nn in Nodes,hh in Hours, ss in Scenarios | nn>1 and nn <= (ScenariosDay1+1) and ss > (nn-2)*nScenarios/ScenariosDay1 and ss <= (nn-1)*nScenarios/ScenariosDay1) do
	Anticipativity4(pp,nn,2,hh,ss):=
	 	soldDayAhead(pp,2,hh,ss) = soldDayAheadAnt(pp,nn,hh);
end-do

(!forall(tt in Years, pp in Seasons,nn in Nodes,hh in HoursD3,ss in Scenarios |(nn>(ScenariosDay1+1) and nn<=((ScenariosDay1+ScenariosDay1^2)+1) and ss >(nn-ScenariosDay1-2)*nScenarios/ScenariosDay1^2 and ss <= (nn-ScenariosDay1-1)*nScenarios/ScenariosDay1^2)) do
	Anticipativity4(tt,nn,3,hh,ss):=
	 	soldDayAhead(tt,3,hh,ss) = soldDayAheadAnt(tt,nn,hh);
end-do!)


writeln("\nModel building completed in ", timestamp - timetracker, " seconds");
writeln("\nSolving model...");
timetracker := timestamp;
		
maximize(NetPresentValue);

writeln("\nModel solved in ", timestamp - timetracker," seconds");

end-model
